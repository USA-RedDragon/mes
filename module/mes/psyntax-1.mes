;;; -*-scheme-*-

;;; Mes --- Maxwell Equations of Software
;;; Copyright Â© 2016 Jan Nieuwenhuizen <janneke@gnu.org>
;;;
;;; This file is part of Mes.
;;;
;;; Mes is free software; you can redistribute it and/or modify it
;;; under the terms of the GNU General Public License as published by
;;; the Free Software Foundation; either version 3 of the License, or (at
;;; your option) any later version.
;;;
;;; Mes is distributed in the hope that it will be useful, but
;;; WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;; GNU General Public License for more details.
;;;
;;; You should have received a copy of the GNU General Public License
;;; along with Mes.  If not, see <http://www.gnu.org/licenses/>.

;;; Commentary:

;;; psyntax-1.mes - post psyntax.pp hook.  psyntax-1.mes is loaded
;;; after psyntax-pp.mes.

;;; Code:

(define datum->syntax datum->syntax-object)
(define syntax->datum syntax-object->datum)

(define-macro (define-syntax macro-name transformer)
  `(define-macro ,macro-name
     `(lambda args
        (eval
         (syntax-object->datum
          (,transformer (cons ,macro-name args)))
         (current-module)))))

(define-syntax syntax-rules
  (lambda (x)
    (syntax-case x ()
      ((_ (k ...) ((keyword . pattern) template) ...)
       (syntax (lambda (x)
                (syntax-case x (k ...)
                  ((dummy . pattern) (syntax template))
                  ...)))))))

(define-macro (define-syntax-rule id-pattern . template)
  `(define-syntax ,(car id-pattern)
     (syntax-rules ()
       ((,(car id-pattern) . ,(cdr id-pattern)) ,@template))))

(define-macro (let-syntax bindings . rest)
  `((lambda ()
      ,@(map (lambda (binding)
               `(define-macro ,(car binding)
                  `(lambda args
                     (eval
                      (syntax-object->datum
                       (,(cadr binding) (cons ',(car binding) args)))
                      (current-module)))))
             bindings)
      ,@rest)))
