;;; -*-scheme-*-

;;; Mes --- Maxwell Equations of Software
;;; Copyright Â© 2016 Jan Nieuwenhuizen <janneke@gnu.org>
;;;
;;; mes.mes: This file is part of Mes.
;;;
;;; Mes is free software; you can redistribute it and/or modify it
;;; under the terms of the GNU General Public License as published by
;;; the Free Software Foundation; either version 3 of the License, or (at
;;; your option) any later version.
;;;
;;; Mes is distributed in the hope that it will be useful, but
;;; WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;; GNU General Public License for more details.
;;;
;;; You should have received a copy of the GNU General Public License
;;; along with Mes.  If not, see <http://www.gnu.org/licenses/>.

;; The Maxwell Equations of Software -- John McCarthy page 13
;; http://www.softwarepreservation.org/projects/LISP/book/LISP%201.5%20Programmers%20Manual.pdf

(define (caar x) (car (car x)))
(define (cadr x) (car (cdr x)))
(define (cdar x) (cdr (car x)))
(define (cddr x) (cdr (cdr x)))
(define (caadr x) (car (car (cdr x))))
(define (caddr x) (car (cdr (cdr x))))
(define (cddar x) (cdr (cdr (car x))))
(define (cdadr x) (cdr (car (cdr x))))
(define (cadar x) (car (cdr (car x))))
(define (cdddr x) (cdr (cdr (cdr x))))

;; Page 12
(define (pairlis x y a)
  (cond
   ((null? x) a)
   ((atom? x) (cons (cons x y) a))
   (#t (cons (cons (car x) (car y))
             (pairlis (cdr x) (cdr y) a)))))

(define (assq x a)
  (cond
   ((null? a) #f)
   ((eq? (caar a) x) (car a))
   (#t (assq x (cdr a)))))

;; Page 13
(define (evcon c a)
  (cond
   ((null? c) *unspecified*)
   ;; single-statement cond
   ;; ((eval (caar c) a) (eval (cadar c) a))
   ((eval (caar c) a)
    (cond ((null? (cddar c)) (eval (cadar c) a))
          (#t (eval (cadar c) a)
              (evcon
               (cons (cons #t (cddar c)) '())
               a))))
   (#t (evcon (cdr c) a))))

(define (evlis m a)
  (cond
   ((null? m) '())
   (#t (cons (eval (car m) a) (evlis (cdr m) a)))))


(define (apply-env fn x a) 
  (cond
   ((atom? fn)
    (cond
     ((eq? fn 'current-module)
      (c:apply-env current-module '() a))
     ((eq? fn 'call-with-values)
      (c:apply-env 'call-with-values x a))
     ((builtin? fn)
      (call fn x))
     (#t (apply-env (eval fn a) x a))))
   ((eq? (car fn) 'lambda)
    (begin-env (cddr fn) (pairlis (cadr fn) x a)))
   ((eq? (car fn) 'label) (apply-env (caddr fn) x (cons (cons (cadr fn)
                                                         (caddr fn)) a)))))

(define (begin-env body a)
  (cond ((null? body) *unspecified*)
        ((null? (cdr body)) (eval (car body) a))
        (#t (eval (car body) a)
            (begin-env (cdr body) a))))

(define (set-env! x e a)
  (set-cdr! (assq x a) e))

(define (eval e a)
  (cond
   ((eq? e #t) #t)
   ((eq? e #f) #f)
   ((char? e) e)
   ((number? e) e)
   ((string? e) e)
   ((vector? e) e)
   ((atom? e) (cdr (assq e a)))
   ((builtin? e) e)
   ((atom? (car e))
    (cond
     ((eq? (car e) 'quote) (cadr e))
     ((eq? (car e) 'begin) (begin-env (cdr e) a))
     ((eq? (car e) 'lambda) e)
     ((eq? (car e) 'set!) (set-env! (cadr e) (caddr e) a))
     ((eq? (car e) 'unquote) (eval (cadr e) a))
     ((eq? (car e) 'quasiquote) (eval-quasiquote (cadr e) a))
     ((eq? (car e) 'cond) (evcon (cdr e) a))
     ((pair? (assq (car e) (cdr (assq '*macro* a))))
      (c:eval
       (c:apply-env
        (cdr (assq (car e) (cdr (assq '*macro* a))))
        (cdr e)
        a)
       a))
     (#t (apply-env (car e) (evlis (cdr e) a) a))))
   (#t (apply-env (car e) (evlis (cdr e) a) a))))

(define (eval-quasiquote e a)
  (cond ((null? e) e)
        ((atom? e) e)
        ((atom? (car e)) (cons (car e) (eval-quasiquote (cdr e) a)))
        ((eq? (caar e) 'unquote) (cons (eval (cadar e) a) '()))
        ((eq? (caar e) 'quote) (cons (cadar e) '()))
        ((eq? (caar e) 'quasiquote) (cons (cadar e) '()))
        (#t (cons (car e) (eval-quasiquote (cdr e) a)))))

(define (readenv a)
  (readword (read-byte) '() a))

(define (readword c w a)
  (cond ((eq? c -1) ;; eof
         (cond ((eq? w '()) '())
               (#t (lookup w a))))
        ((eq? c 10) ;; \n
         (cond ((eq? w '()) (readword (read-byte) w a))
               ;; DOT ((eq? w '(*dot*)) (car (readword (read-byte) '() a)))
               (#t (lookup w a))))
        ((eq? c 32) ;; \space
         (readword 10 w a))
        ((eq? c 40) ;; (
         (cond ((eq? w '()) (readlist a))
               (#t (unread-byte c) (lookup w a))))
        ((eq? c 41) ;; )
         (cond ((eq? w '()) (unread-byte c) w)
               (#t (unread-byte c) (lookup w a))))
        ((eq? c 39) ;; '
         (cond ((eq? w '())
                (cons (lookup (cons c '()) a)
                      (cons (readword (read-byte) w a) '())))
               (#t (unread-byte c) (lookup w a))))
        ((eq? c 59) ;; ;
         (readcomment c)
         (readword 10 w a))
        ((eq? c 35) ;; #
         (cond ((eq? (peek-byte) 33) ;; !
                (read-byte)
                (readblock (read-byte))
                (readword 10 w a))
               ;; TODO: char, vector
               (#t (readword (read-byte) (append2 w (cons c '())) a))))
        (#t (readword (read-byte) (append2 w (cons c '())) a))))

(define (readblock c)
  (cond ((eq? c 33) (cond ((eq? (peek-byte) 35) (read-byte))
                         (#t (readblock (read-byte)))))
        (#t (readblock (read-byte)))))

(define (eat-whitespace)
  (cond ((eq? (peek-byte) 10) (read-byte) (eat-whitespace))
        ((eq? (peek-byte) 32) (read-byte) (eat-whitespace))
        ((eq? (peek-byte) 35) (read-byte) (eat-whitespace))
        (#t #t)))

(define (readlist a)
  (eat-whitespace)
  (cond ((eq? (peek-byte) 41) ;; )
         (read-byte)
         '())
        ;; TODO *dot*
        (#t (cons (readword (read-byte) '() a) (readlist a)))))

(define (readcomment c)
  (cond ((eq? c 10) ;; \n
         c)
        (#t (readcomment (read-byte)))))
