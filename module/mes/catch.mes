;;; -*-scheme-*-

;;; Mes --- Maxwell Equations of Software
;;; Copyright Â© 2016,2018 Jan Nieuwenhuizen <janneke@gnu.org>
;;;
;;; This file is part of Mes.
;;;
;;; Mes is free software; you can redistribute it and/or modify it
;;; under the terms of the GNU General Public License as published by
;;; the Free Software Foundation; either version 3 of the License, or (at
;;; your option) any later version.
;;;
;;; Mes is distributed in the hope that it will be useful, but
;;; WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;; GNU General Public License for more details.
;;;
;;; You should have received a copy of the GNU General Public License
;;; along with Mes.  If not, see <http://www.gnu.org/licenses/>.

(mes-use-module (mes let))
(mes-use-module (mes fluids))

(define %eh (make-fluid
             (lambda (key . args)
               (if (defined? 'simple-format)
                   (simple-format (current-error-port) "unhandled exception:~a:~a\n" key args)
                   (begin
                     (display "unhandled exception:" (current-error-port))
                     (display key (current-error-port))
                     (display ":" (current-error-port))
                     (write args (current-error-port))
                     (newline (current-error-port))))
               (exit 1))))

(define (catch key thunk handler)
  (let ((previous-eh (fluid-ref %eh)))
    (with-fluid*
        %eh #f
        (lambda ()
          (call/cc
           (lambda (cc)
             (fluid-set! %eh
                         (lambda (k . args)
                           (let ((handler (if (or (eq? key #t) (eq? key k)) handler
                                              previous-eh)))
                             (cc
                              (lambda (x)
                                (apply handler (cons k args)))))))
             (thunk)))))))

(define (throw key . args)
  (let ((handler (fluid-ref %eh)))
    (apply handler (cons key args))))

(define with-throw-handler catch) ; FIXME: hack for Nyacc 0.75
